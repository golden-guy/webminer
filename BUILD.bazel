# Copyright (c) 2022 Mark Friedenbach
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.

load("@rules_cc//cc:defs.bzl", "cc_library")

# cross-compile settings
config_setting(
    name = "armv7hf",
    values = {
        "cpu": "armeabi-v7a",
    },
)

config_setting(
    name = "aarch64",
    values = {
        "cpu": "aarch64-linux-gnu",
    },
)

cc_library(
    name = "common",
    hdrs = [
        "compat/byteswap.h",
        "compat/cpuid.h",
        "compat/endian.h",
        "crypto/common.h",
        "support/allocators/secure.h",
        "support/cleanse.h",
        "support/lockedpool.h",
        "util/macros.h",
    ],
    srcs = [
        "support/cleanse.cc",
        "support/lockedpool.cc",
    ],
)

cc_library(
    name = "chacha20",
    hdrs = [
        "crypto/chacha20.h",
    ],
    srcs = [
        "crypto/chacha20.cc",
    ],
    deps = [
        ":common",
    ],
)

cc_library(
    name = "random",
    hdrs = [
        "random.h",
        "randomenv.h",
    ],
    srcs = [
        "random.cc",
        "randomenv.cc",
    ],
    deps = [
        "@com_google_absl//absl/time:time",
        ":chacha20",
        ":sha2",
        ":sync",
        ":uint256",
    ],
)

cc_library(
    name = "sha2",
    copts = select({
        ":aarch64": [
            "-mcpu=cortex-a72+crypto",
            "-march=armv8-a+crypto",
            "-mtune=cortex-a72",
        ],
        ":armv7hf": [
            "-march=armv7-a",
            "-mfpu=neon-vfpv4",
            "-mtune=cortex-a53",
        ],
        # assume x86_64 as default
        "//conditions:default": [
            "-msse4",
            "-msse4.1",
            "-mavx2",
            "-msha",
        ],
    }),
    defines = select({
        ":aarch64": [
            "USE_ASM",
        ],
        ":armv7hf": [],
        # assume x86_64 as default
        "//conditions:default": [
            "USE_ASM",
            "ENABLE_SSE41",
            "ENABLE_AVX2",
            "ENABLE_SHANI",
        ],
    }),
    hdrs = [
        "crypto/sha256.h",
        "crypto/sha512.h",
    ],
    srcs = [
        "crypto/sha256.cc",
        "crypto/sha256_sse4.cc",
        "crypto/sha256_sse41.cc",
        "crypto/sha256_avx2.cc",
        "crypto/sha256_shani.cc",
        "crypto/sha512.cc",
    ],
    deps = [
        ":common",
    ],
)

cc_library(
    name = "sync",
    hdrs = [
        "sync.h",
        "threadsafety.h",
    ],
    srcs = [
        "sync.cc",
    ],
    deps = [
        ":common",
    ],
)

cc_library(
    name = "uint256",
    hdrs = [
        "uint256.h",
    ],
    srcs = [
        "uint256.cc",
    ],
    deps = [
        "@com_google_absl//absl/strings:strings",
    ],
)

load("@rules_cc//cc:defs.bzl", "cc_binary")

cc_binary(
    name = "webminer",
    srcs = ["main.cc"],
    defines = select({
        ":aarch64": [
            "USE_OPENSSL_ASM",
        ],
        ":armv7hf": [
            "USE_OPENSSL_ASM",
        ],
        "//conditions:default": [],
    }),
    deps = [
        "@com_google_absl//absl/flags:parse",
        "@com_google_absl//absl/flags:usage",
        "@com_google_absl//absl/strings:strings",
        "@boringssl//:crypto",
        ":cpp_http",
        ":random",
        ":sha2",
        ":uint256",
        ":univalue",
    ],
)

load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

cmake(
    name = "cpp_http",
    deps = [
        "@boringssl//:ssl",
    ],
    lib_source = "@cpp_http//:all_srcs",
    install = True,
    out_headers_only = True,
)

configure_make(
    name = "univalue",
    configure_in_place = True, # Required by autogen=True
    autogen = True,
    configure_options = select({
        ":aarch64": [
            "--host=aarch64-linux-gnu",
        ],
        ":armv7hf": [
            "--host=armv7l-unknown-linux-gnueabihf",
        ],
        # assume x86_64 as default
        "//conditions:default": [],
    }),
    env = {
        "AR": "",
    },
    lib_source = "@univalue//:all_srcs",
    out_static_libs = ["libunivalue.a"],
)

# End of File
